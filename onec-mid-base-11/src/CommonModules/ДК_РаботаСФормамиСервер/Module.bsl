#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	// ++ Симбирёва 02.12.2025
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьполеКонтактноеЛицоГруппаШапкаПраво(Форма);
		ДобавитьГруппуРеквизитовСкидкаНаФорму(Форма);
		ДобавитьПолеСогласованнаяСкидкаНаФормуПередСуммаДокумента(Форма);
		ДобавитьКомандуПересчитатьСтрокуНаФорму(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьполеКонтактноеЛицоГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда  
		ДобавитьполеКонтактноеЛицоГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	// -- Симбирёва 02.12.2025

КонецПроцедуры



#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьполеКонтактноеЛицоГруппаШапкаПраво(Форма) 

// ++ Симбирёва 02.12.2025
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
    ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
// -- Симбирёва 02.12.2025

КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
// ++ Симбирёва 02.12.2025
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_КонтактноеЛицо";
// -- Симбирёва 02.12.2025

КонецПроцедуры 

Процедура ДобавитьГруппуРеквизитовСкидкаНаФорму(Форма)
	
// ++ Симбирёва 02.12.2025
	ГруппаРеквизитовСкидка = Форма.Элементы.Добавить("ГруппаРеквизитовСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево); 
	ГруппаРеквизитовСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаРеквизитовСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаРеквизитовСкидка.ОтображатьЗаголовок = Ложь; 
	ГруппаРеквизитовСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
// -- Симбирёва 02.12.2025
	
КонецПроцедуры  

Процедура ДобавитьПолеСогласованнаяСкидкаНаФормуПередСуммаДокумента(Форма)
	
// ++ Симбирёва 02.12.2025
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаРеквизитовСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДК_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ЗадатьВопросПользователю"); 
// -- Симбирёва 02.12.2025
	
КонецПроцедуры

Процедура ДобавитьКомандуПересчитатьСтрокуНаФорму(Форма) Экспорт
	
// ++ Симбирёва 02.12.2025
	КомандаОбновить = Форма.Команды.Добавить("Обновить");  
	КомандаОбновить.Заголовок = "Обновить";
	КомандаОбновить.Действие = "КомандаОбновить";
	
	КнопкаКоманды = Форма.Элементы.Добавить("КомандаОбновить", Тип("КнопкаФормы"), Форма.Элементы.ГруппаРеквизитовСкидка);
    КнопкаКоманды.ИмяКоманды = "Обновить";
	КнопкаКоманды.Заголовок = "Пересчитать таблицу";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаКоманды.Картинка = БиблиотекаКартинок.Обновить;  
	КнопкаКоманды.Отображение = ОтображениеКнопки.КартинкаИТекст;
// -- Симбирёва 02.12.2025

КонецПроцедуры

#КонецОбласти
